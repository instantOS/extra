# Maintainer: paperbenni <paperbenni@gmail.com>

pkgname=st-instantos
_pkgname=st-instantos
pkgver=202101232339
pkgrel=1
pkgdesc='st fork used by instantOS'
url='https://github.com/instantOS/st-instantos.git'
arch=('any')
license=('MIT')
options=('zipman')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=('st')
conflicts=(st)

# include config.h and any patches you want to have applied here
source=("${_pkgname}::git+$url")
sha256sums=('SKIP')

pkgver() {
    cd $_pkgname
    echo "$(date '+%Y%m%d%H%M')"
}

prepare() {
    cd "${_pkgname}"
    # skip terminfo which conflicts with nsurses
    sed -i '/tic /d' Makefile
}

build() {
    cd "${_pkgname}"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "${_pkgname}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
